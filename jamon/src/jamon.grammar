Package
  org.modusponens.jtt;

Helpers
  letter = ['a'..'z'] | ['A'..'Z'];
  digit = ['0'..'9'];
  letter_or_digit = letter | digit | '_';
  period = '.';
  ht  = 0x0009;
  lf  = 0x000a;
  ff  = 0x000c;
  cr  = 0x000d;
  sp  = ' ';
  nl = lf | cr | cr lf;
  hs = sp | ht;
  ws = hs | lf | ff;

  percent = '%';
  all = [0x0000 .. 0xFFFF];
  all_except_nl = [all - [lf + cr]];

States
  start,
  body,
  imports,
  emit,
  java,
  jline,
  call,
  argexp,
  callexp,
  args;

Tokens
  {start -> args,body -> args} args_start = '<%args>';
  {args -> body} args_end = '</%args>';
  {start ->imports,body -> imports} import_start = '<%import>';
  {imports -> body} import_end = '</%import>';
  {start ->java,body -> java} java_start = '<%java>' ws*;
  {java -> body} java_end = ws* '</%java>';
  {args,imports} dot = period;
  {args -> argexp} arrow = '=>' hs*;
  {call,args,imports} white_space = ws*;
  {call,args,imports} identifier = letter letter_or_digit*;


  {start -> emit,body -> emit} emit_start = '<%' ws*;
  {emit -> body} emit_end = ws* '%>';

  {start -> jline} injava = percent hs*;
  {jline -> start} outjava = hs* nl;
  {jline,argexp} fragment = all_except_nl*;
  {argexp -> args} donearg = hs* nl;

  {emit,java} any = all;
  {body,start -> body} text = all_except_nl;
  {start,body -> start} newline = nl;

  {args} brackets = '[' (hs)* ']';

  {call -> callexp} carrow = '=>' hs*;
  {start -> call,body -> call} call_start = '<&' ws*;
  {call -> body} call_end = ws* '&>';
  {callexp -> call} param_expr = ([all - [';' + '&']] | '&' [all-'>'])*;
  {call} semi = ';';
  {call} slash = '/';

Ignored Tokens

  white_space;

Productions

  template = component*;

  component = {body} text | {newline} newline
            | {imports} import_start name* import_end
            | {args} args_start arg* args_end
            | {java} java_start any* java_end
            | {jline} injava fragment outjava
            | {emit} emit_start any* emit_end
            | {call} call_start path param* call_end
            ;

  path = {simple} slash? identifier | {qualified} path slash identifier;

  arg = type [name]: identifier default?;

  param = semi identifier carrow param_expr;

  default = arrow fragment donearg;

  name = {simple} identifier
       | {qualified} name dot identifier;

  type = name brackets*;
