/*
 * The contents of this file are subject to the Mozilla Public
 * License Version 1.1 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of
 * the License at http://www.mozilla.org/MPL/
 *
 * Software distributed under the License is distributed on an "AS
 * IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * rights and limitations under the License.
 *
 * The Original Code is Jamon code, released February, 2003.
 *
 * The Initial Developer of the Original Code is Jay Sachs.  Portions
 * created by Jay Sachs are Copyright (C) 2003 Jay Sachs.  All Rights
 * Reserved.
 *
 * Contributor(s): Ian Robertson
 */

Package
  org.jamon;

Helpers
  lower = ['a'..'z'];
  upper = ['A'..'Z'];
  letter = [[lower + upper] + '_'];
  digit = ['0'..'9'];
  letter_or_digit = letter | digit;
  period = '.';
  ht  = 0x0009;
  lf  = 0x000a;
  ff  = 0x000c;
  cr  = 0x000d;
  sp  = ' ';
  nl = lf | cr | cr lf;
  hs = [sp + ht];
  ws = [[[hs + lf] + ff] + cr];
  argsep = ';'; // could be nl ...
  all = [0x0000 .. 0xFFFF];
  all_except_nl = [all - [lf + cr]];
  q = '"';
  bs = '\';
  pipe = '|';
  quoted = q ([all-[q+bs]]* | (bs all))* q;
  sq = 39;
  hex = [digit + [['A' .. 'F'] + ['a' .. 'f']]];
  squoted = sq (([all-[sq+bs]]) | (bs all) | (bs 'u' hex hex hex hex)) sq;
  gt = '>';
  lt = '<';
  pct = '%';
  slash = '/';
  hash = '#';

States
  start,
  body,
  def,
  method,
  override,
  imports,
  extends,
  emit,
  class,
  java,
  jline,
  call,
  multifragmentcall,
  multifragmentcallexp,
  multifragmentcallbody,
  argexp,
  callexp,
  args,
  parent_args,
  parent_argexp,
  escape,
  literal,
  alias,
  farginit;

Tokens

  {start -> farginit,body -> farginit} farg_start_begin = '<%frag';
  {farginit -> args} farg_start_done = '>';
  {farginit -> body} farg_start_end = ws* '/>';
  {args -> body} args_end = '</%args>';
  {args -> body} farg_end = '</%frag>';

  {start -> extends,body -> extends} extends_start = '<%extends' ws+;

  {start -> parent_args, body -> parent_args}
         inherited_args_start = '<%xargs>';
  {parent_args -> body} inherited_args_end = '</%xargs>';

  {start -> class,body -> class} class_start = '<%class>' ws*;
  {class -> body} class_end = ws* '</%class>';
  {start -> args, body -> args} args_start = '<%args>';
  {start -> imports,body -> imports} import_start = '<%import>';
  {imports -> body} import_end = '</%import>';
  {start -> imports,body -> imports} implements_start = '<%implements>';
  {imports -> body} implements_end = '</%implements>';
  {start ->java,body -> java} java_start = '<%java>' ws*;
  {java -> body} java_end = ws* '</%java>';
  {args,imports} dot = period;
  {args -> argexp, parent_args -> parent_argexp,alias} arrow = '=>' hs*;
  {call,multifragmentcall,multifragmentcallbody,args,farginit,imports,parent_args,extends,alias} white_space = ws*;
  {call,multifragmentcall,multifragmentcallbody,args,parent_args,farginit,imports,def,extends,method,override,alias} identifier = letter letter_or_digit*;
  {call} child_call = '*CHILD';

  {start -> emit,body -> emit} emit_start = '<%' ws+;
  {emit -> body, escape -> body} emit_end = ws* '%>';
  {emit -> escape} escape_delimiter = hash;
  {escape} escape_code = letter;

  {emit} emit_expr =
      ( [all - [[[q + pct] + hash] + sq]]
      | quoted
      | squoted
      | pct [all - gt]
      )*;

  {start -> jline} injava = pct hs*;
  {jline -> start} outjava = hs* nl;
  {jline} expr = all_except_nl*;
   // fixme: line ending ...
  {argexp, parent_argexp} argexpr = ([all - [argsep+q]] | quoted | squoted)*;
  {argexp -> args,parent_argexp -> parent_args, args, parent_args}
      donearg = hs* argsep ws*;

  {java} java_stmts =
       ( [all - [q + lt]]
       | quoted
       | squoted
       | '<' [all - slash]
       | '</' [all - '%']
       | '</%' [all - 'j']
       | '</%j' [all - 'a']
       | '</%ja' [all - 'v']
       | '</%jav' [all - 'a']
       | '</%java' [all - gt] )*;

  {class} class_content =
       ( [all - [q + lt]]
       | quoted
       | squoted
       | '<' [all - slash]
       | '</' [all - '%']
       | '</%' [all - 'c']
       | '</%c' [all - 'l']
       | '</%cl' [all - 'a']
       | '</%cla' [all - 's']
       | '</%clas' [all - 's']
       | '</%class' [all - gt] )*;

  {body,start -> body} text = all_except_nl;
  {start,body -> start} newline = nl;

  {body -> literal, start -> literal} litstart = '<%LITERAL>';
  {literal} literal_text =
       (    [all - '<']
       | '<' [all - '/']
       | '</' [all - '%']
       | '</%' [all - 'L']
       | '</%L' [all - 'I']
       | '</%LI' [all - 'T']
       | '</%LIT' [all - 'E']
       | '</%LITE' [all - 'R']
       | '</%LITER' [all - 'A']
       | '</%LITERA' [all - 'L']
       | '</%LITERAL' [all - '>'] )*;
  {literal -> body} litend = '</%LITERAL>';

  {args} brackets = '[' (hs)* ']';
  {start -> body,body,multifragmentcallbody -> body} fragment_call_end = '</&>';
  {call -> callexp, multifragmentcall -> multifragmentcallexp} carrow = '=>' hs*;
  {start -> call, body -> call} fragment_call_start = '<&|' ws*;
  {start -> call,body -> call} call_start = '<&' ws*;
  {multifragmentcall -> multifragmentcallbody, call -> body} call_end = ws* '&>';
  {callexp -> call, multifragmentcallexp -> multifragmentcall} param_expr =
       ([all - [argsep + [q + '&']]] | quoted | squoted | '&' [all - '>'])*;
  {multifragmentcall,call,imports,alias} semi = ';';
  {multifragmentcall,call,extends,alias} pathsep = slash;
  {multifragmentcall,call,extends,alias} updir = period period slash;

  {start -> multifragmentcall, body -> multifragmentcall} multi_fragment_call_init = '<&||' ws*;


  {multifragmentcallbody} named_farg_init = '<|';
  {multifragmentcallbody -> body} named_farg_done = '>';
  {body -> multifragmentcallbody} named_farg_end = '</|>';

  {start -> def,body -> def} def_start = '<%def' hs+;
  {body,start} def_end = '</%def>';
  {def -> body, method -> body, override -> body, extends -> body} gt = hs* gt;

  {start -> method,body -> method} method_start = '<%method' hs+;
  {body,start} method_end = '</%method>';

  {start -> override,body -> override} override_start = '<%override' hs+;
  {body,start} override_end = '</%override>';

  {start -> body,body} doc =
       '<%doc>' ([all - '<']
              | '<' [all-slash]
              | '</' [all-'%']
              | '</%' [all-'d']
              | '</%d' [all-'o']
              | '</%do' [all-'c']
              | '</%doc' [all-'>'])* '</%doc>';

  {start -> alias, body -> alias} alias_start = '<%aliases>';
  {alias -> body} alias_end = '</%aliases>';

Ignored Tokens

  white_space,doc;

Productions

  template = component* partial_jline? ;

  base_component =
              {body} text
            | {newline} newline
            | {literal} litstart literal_text litend
            | {java} java
            | {jline} jline
            | {emit} emit
            | {call} call
            | {fragment_call} fragment_call
            | {multi_fragment_call} multi_fragment_call
            | {args} args
            | {farg} farg
            ;

  component = base_component
            | {alias} aliases
            | {imports} imports
            | {implements} implements
            | {def} def
            | {class} class_tag
            | {extends} extends_start path gt
            | {method} method
            | {override} override
            | {inherited_use} inherited_use
            ;

  aliases = alias_start alias* alias_end;

  alias = alias_name arrow path semi;

  alias_name = {root} pathsep | {id} identifier;

  override_component = {base} base_component | {inherited_use} inherited_use;

  def = def_start identifier gt base_component* def_end;

  method = method_start identifier gt base_component* method_end;

  override = override_start identifier gt override_component* override_end;

  call = call_start path param* call_end
       | {child} call_start child_call call_end
       ;

  fragment_call = fragment_call_start path param* call_end base_component* fragment_call_end;

  multi_fragment_call = multi_fragment_call_init path param* call_end named_farg* fragment_call_end;
  named_farg = named_farg_init identifier named_farg_done base_component* named_farg_end;

  imports = import_start import* import_end;

  implements = implements_start implement* implements_end;

  import = name semi;

  implement = name semi;

  args = args_start arg* args_end;

  class_tag = class_start class_content* class_end;

  farg = farg_start arg* farg_end
       | {argless} farg_tag;

  farg_start = farg_start_begin identifier farg_start_done;

  farg_tag = farg_start_begin identifier farg_start_end;

  java = java_start java_stmts java_end;

  jline = injava expr outjava;

  partial_jline = injava expr;

  emit = emit_start emit_expr escape? emit_end;

  escape = escape_delimiter escape_code;

  path = {alias} aliased_path
       | {rel} updir_path* relative_path
       | {abs} absolute_path;

  relative_path = identifier absolute_path?;

  absolute_path = pathsep relative_path;

  aliased_path = identifier? pathsep absolute_path;

  updir_path = updir;

  arg = type [name]: identifier default? donearg;

  parent_arg = [name]: identifier default? donearg;

  param = semi identifier carrow param_expr;

  default = arrow argexpr;

  name = {simple} identifier
       | {qualified} name dot identifier;

  type = name brackets*;

  inherited_use = inherited_args_start parent_arg* inherited_args_end;
